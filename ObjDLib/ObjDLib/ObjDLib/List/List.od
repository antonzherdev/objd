package core.chain

class List<T> extends Seq<T> {
    static def apply<T> : List<T> = EmptyList.instance.cast<List<T>>
    static def apply<T>(item : T) : List<T> = FilledList<T>(item, EmptyList.instance)
    static def apply<T>(item : T, tail : List<T>) : List<T>  = FilledList<T>(item, tail)

    def iterator : Iterator<T> = {
        val i = ListIterator<T>()
        i.list = self
        i
    }
    def tail : List<T>
    def filter(f : T -> bool) : List<T>
    def reverse : List<T>
}

class FilledList<T>(head : T, tail : List<T>) extends List<T> {
    def headOpt : T? = head
    val count : uint = tail.count + 1
    def isEmpty = false
    def filter(f : T -> bool) : List<T> =
        if(f(head)) FilledList<T>(head, tail.filter(f))
        else tail.filter(f)

    def reverse : List<T> = {
        var ret = FilledList<T>(head, EmptyList.instance)
        var list = tail
        while(!list.isEmpty) {
            ret = FilledList<T>(list.cast<FilledList<T>>.head, ret)
            list = list.tail
        }
        ret
    }

    def for(each : T -> void) {
        var list : FilledList<T> = self
        while(true) {
            each(list.head)
            val tail = list.tail
            if(tail.isEmpty) return nil
            list = tail.cast<FilledList<T>>
        }
    }
}

class EmptyList<T> extends List<T> {
    def count : uint = 0
    def head : T = throw "List is empty"
    def headOpt : T? = nil
    def tail : List<T> = self
    def isEmpty = true

    static val instance = EmptyList<any>()
    def filter(f : T -> bool) : List<T> = self
    def reverse : List<T> = self
    def for(each : T -> void) {
    }
}

class ListIterator<T> extends Iterator<T> {
    var list : List<T>
    def hasNext = !list.isEmpty

    def next : T = {
        val ret = list.head
        list = list.tail
        ret
    }
}