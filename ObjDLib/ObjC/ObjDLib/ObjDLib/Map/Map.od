package core.chain

trait Map<K, V> extends Iterable<(K, V)> {
    def apply(key : K) : V?
    def keys : Iterable<K>
    def values : Iterable<V>
    def contains(key : K) : bool = apply(key) != nil
    def isValueEqual(key : K, value : V) : bool = apply(key).map(_ == value).getOr(false)
}

trait ImMap<K, V> extends Map<K, V> with ImIterable<(K, V)> {
    def add(item : (K, V)) : ImMap<K, V> = {
        val builder = HashMapBuilder<K, V>()
        builder.appendAll(self)
        builder.append(item)
        builder.build
    }

    override def mCopy : MMap<K, V> = {
        val m = MHashMap<K, V>()
        m.assign(self)
        m
    }
}

trait MMap<K, V> extends Map<K, V> with MIterable<(K, V)> {
    def set(key : K, value : V)
    def remove(key : K) : V?
    def apply(key : K, orUpdateWith: () -> V) : V = apply(key).getOr {
        val init : V = orUpdateWith()
        set(key, init)
        init
    }
    def modify(key : K, by : V? -> V?) : V? = {
        val newObject = by(apply(key))
        if(newObject == nil) remove(key)
        else set(key, newObject)
        newObject
    }

    override def append(item : (K, V)) {
        set(item.b, item.a)
    }
    override def remove(item : (K, V)) : bool = remove(item.a) != nil

    override def im : ImMap<K, V> = imCopy
    override def imCopy : ImMap<K, V> = {
        val arr = MHashMap<K, V>()
        for{item -> arr.set(item.a, item.b)}
        arr.im
    }

    def assign(imMap : ImMap<K, V>) {
        clear
        imMap.for(append(_))
    }
}

class ImMapDefault<K, V>(val map : ImMap<K, V>, val defaultFunc : K -> V) extends ImIterable<(K, V)> {
    override def count : uint = map.count
    override def iterator : Iterator<(K, V)> = map.iterator
    def apply(key : K) : V = map.apply(key).getOr(defaultFunc(key))
    def keys : Iterable<K> = map.keys
    def values : Iterable<V> = map.values
    def contains(key : K) : bool = map.contains(key)
    def isEqual(map : Map<K, V>) = self.map == map
    def isEqual(mapDefault : ImMapDefault<K, V>) = map == mapDefault.map
    override def hash : uint = map.hash
    override def mCopy : MMapDefault<K, V> = MMapDefault<K, V>(map.mCopy, defaultFunc)
}
class MMapDefault<K, V>(val map : MMap<K, V>, val defaultFunc : K -> V) extends MIterable<(K, V)> {
    override def count : uint = map.count
    override def iterator : Iterator<(K, V)> = map.iterator
    override def mutableIterator : MIterator<(K, V)> = map.mutableIterator
    def apply(key : K) : V = map(key, orUpdateWith = defaultFunc(key))
    def keys : Iterable<K> = map.keys
    def values : Iterable<V> = map.values
    def contains(key : K) : bool = map.contains(key)
    def set(key : K, value : V) {
        map.set(key, value)
    }
    def modify(key : K, by : V -> V) : V {
        val value = by(apply(key))
        map.set(key, value)
        value
    }
    override def append(item : (K, V)) {
        map.append(item)
    }
    def remove(key : K) : V? = map.remove(key)
    override def remove(item : (K, V)) : bool = map.remove(item = item)
    override def clear {
        map.clear
    }

    override def im : ImMapDefault<K, V> = ImMapDefault(map.im, defaultFunc)
    override def imCopy : ImMapDefault<K, V> = ImMapDefault(map.imCopy, defaultFunc)
}

class HashMapBuilder<K, V> extends Builder<(K, V), ImHashMap<K, V>> {
    private val map = MHashMap<K, V>()

    override def append(item : (K, V)) {
        map.set(item.a, item.b)
    }
    override def build : ImHashMap<K, V> = map.im
}