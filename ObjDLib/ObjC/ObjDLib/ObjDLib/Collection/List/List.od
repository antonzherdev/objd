package objd.collection

abstract class ImList<T> extends ImSeq<T> {
    static def apply<T> : ImList<T> = EmptyList.instance.cast<ImList<T>>
    static def apply<T>(item : T) : ImList<T> = FilledList<T>(item, EmptyList.instance.cast<ImList<T>>)
    static def apply<T>(item : T, tail : ImList<T>) : ImList<T>  = FilledList<T>(item, tail)

    override def iterator : Iterator<T> = {
        val i = ListIterator<T>()
        i.list = self
        i
    }
    override def tail : ImList<T>
    def filter(f : T -> bool) : ImList<T>
    def reverse : ImList<T>

    def insert<C extends Comparable<C>>(self : ImList<C>, item : C) : ImList<T>
}

case class FilledList<T>(val _head : T, override val tail : ImList<T>) extends ImList<T> {
    override def head : T? = _head
    override val count : uint = tail.count + 1
    override def isEmpty = false
    override def filter(f : T -> bool) : ImList<T> =
        if(f(_head)) FilledList<T>(_head, tail.filter(f))
        else tail.filter(f)

    override def reverse : ImList<T> = reverseAndAdd(EmptyList.instance.cast<ImList<T>>)
    private def reverseAndAdd(list : ImList<T>) : ImList<T> = {
        var ret = FilledList<T>(_head, list)
        var l = tail
        while(!l.isEmpty) {
            ret = FilledList<T>(l.cast<FilledList<T>>._head, ret)
            l = l.tail
        }
        ret
    }

    override def for(each : T -> void) {
        var list : FilledList<T> = self
        while(true) {
            each(list._head)
            val tail = list.tail
            if(tail.isEmpty) return nil
            list = tail.cast<FilledList<T>>
        }
    }

    override def insert<C extends Comparable<C>>(self : FilledList<C>, item : C) : ImList<T> = {
        var before = ImList<T>()
        var list : FilledList<T> = self
        while(true) {
            val h = list._head
            if(item.compare(h.cast<C>) < 0) {
                return FilledList(item.cast<T>, before).reverseAndAdd(list)
            }
            before = ImList<T>(h, before)

            if(list.tail.isEmpty) {
                return FilledList(item.cast<T>, before).reverse
            }
            list = list.tail.cast<FilledList<T>>
        }
    }
}

case class EmptyList<T> extends ImList<T> {
    override def count : uint = 0
    override def head : T? = nil
    override def tail : ImList<T> = self
    override def isEmpty = true

    static val instance = EmptyList<any>()
    override def filter(f : T -> bool) : ImList<T> = self
    override def reverse : ImList<T> = self
    override def for(each : T -> void) {}

    override def insert<C extends Comparable<C>>(self : ImList<C>, item : C) : ImList<T> = ImList(item.cast<T>)
}

class ListIterator<T> extends Iterator<T> {
    var list : ImList<T> = EmptyList.instance.cast<ImList<T>>
    override def hasNext = !list.isEmpty

    override def next : T = {
        val ret = list.head
        list = list.tail
        ret.get
    }
}

class ImListBuilder<T> extends Builder<T, ImList<T>> {
    private var list = ImList<T>()
    override def append(item : T) {
        list = ImList(item, list)
    }
    override def build : ImList<T> = list.reverse
}